ВЫБРАТЬ
	ЗНАЧЕНИЕ(Справочник.смтр_ДополнительныеНастройки.Справочник_НаправленияДеятельности_Заготовка) КАК НДЗаготовка
ПОМЕСТИТЬ ВТ_НДЗаготовка
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЗНАЧЕНИЕ(Справочник.смтр_ДополнительныеНастройки.Справочник_СтатьиКалькуляции_ВозвратныеОтходы) КАК СтатьяВО
ПОМЕСТИТЬ ВТ_СтатьяВО
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЗНАЧЕНИЕ(Справочник.смтр_ДополнительныеНастройки.Справочник_СтатьиКалькуляции_ЗаработнаяПлатаОсновногоПроизводство) КАК СтатьяОТИтого
ПОМЕСТИТЬ ВТ_СтатьяОТИтого
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЗНАЧЕНИЕ(Справочник.смтр_ДополнительныеНастройки.Справочник_СтатьиКалькуляции_ОсновнаяЗаработнаяПлата) КАК СтатьяОТ
ПОМЕСТИТЬ ВТ_СтатьяОТ

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ЗНАЧЕНИЕ(Справочник.смтр_ДополнительныеНастройки.Справочник_СтатьиКалькуляции_ДополнительнаяЗаработнаяПлата)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЗНАЧЕНИЕ(Справочник.смтр_ДополнительныеНастройки.Справочник_СтатьиКалькуляции_ОбщезаводскиеРасходы) КАК СтатьяПроцент
ПОМЕСТИТЬ ВТ_СтатьяПроцент

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ЗНАЧЕНИЕ(Справочник.смтр_ДополнительныеНастройки.Справочник_СтатьиКалькуляции_ЦеховыеРасходы)

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ЗНАЧЕНИЕ(Справочник.смтр_ДополнительныеНастройки.Справочник_СтатьиКалькуляции_ОтчисленияНаСоцнужды)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	смтр_ПлановаяСтоимость.Номенклатура КАК Номенклатура,
	смтр_ПлановаяСтоимость.Характеристика КАК Характеристика,
	смтр_ПлановаяСтоимость.Договор КАК Договор,
	ВЫБОР
		КОГДА смтр_ПлановаяСтоимость.СтатьяКалькуляции <> ВТ_СтатьяВО.СтатьяВО.Значение
			ТОГДА 1
		ИНАЧЕ -1
	КОНЕЦ * смтр_ПлановаяСтоимость.Стоимость КАК СтоимостьВсего,
	ВЫБОР
		КОГДА смтр_ПлановаяСтоимость.Количество = 0
			ТОГДА 1
		ИНАЧЕ смтр_ПлановаяСтоимость.Количество
	КОНЕЦ КАК Количество,
	смтр_ПлановаяСтоимость.СтатьяКалькуляции КАК СтатьяКалькуляции,
	смтр_ПлановаяСтоимость.НомерСпецификации КАК НомерСпецификации,
	смтр_ПлановаяСтоимость.Период КАК Период,
	ВЫБОР
		КОГДА смтр_ПлановаяСтоимость.СтатьяКалькуляции <> ВТ_СтатьяВО.СтатьяВО.Значение
			ТОГДА 1
		ИНАЧЕ -1
	КОНЕЦ * смтр_ПлановаяСтоимость.Стоимость * ВЫБОР
		КОГДА смтр_ПлановаяСтоимость.Коэффициент <> 0
			ТОГДА смтр_ПлановаяСтоимость.Коэффициент
		ИНАЧЕ 0
	КОНЕЦ КАК СтоимостьСКоэффициентом
ПОМЕСТИТЬ ВТ_ПланИсходный
ИЗ
	РегистрСведений.смтр_ПлановаяСтоимость.СрезПоследних(&ДатаОкончания, ) КАК смтр_ПлановаяСтоимость,
	ВТ_СтатьяВО КАК ВТ_СтатьяВО
ГДЕ
	смтр_ПлановаяСтоимость.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	И НЕ смтр_ПлановаяСтоимость.СтатьяКалькуляции В
				(ВЫБРАТЬ
					ВТ_СтатьяОТ.СтатьяОТ.Значение
				ИЗ
					ВТ_СтатьяОТ)
	И (смтр_ПлановаяСтоимость.Период = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
			ИЛИ &ИспользоватьПредыдущиеМесяцы)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ПланИсходный.Номенклатура КАК Номенклатура,
	ВТ_ПланИсходный.Характеристика КАК Характеристика,
	ВТ_ПланИсходный.Договор КАК Договор,
	МАКСИМУМ(ВТ_ПланИсходный.Период) КАК Период
ПОМЕСТИТЬ ВТ_ПланАктуальный
ИЗ
	ВТ_ПланИсходный КАК ВТ_ПланИсходный

СГРУППИРОВАТЬ ПО
	ВТ_ПланИсходный.Номенклатура,
	ВТ_ПланИсходный.Характеристика,
	ВТ_ПланИсходный.Договор
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ПланАктуальный.Номенклатура КАК Номенклатура,
	ВТ_ПланАктуальный.Характеристика КАК Характеристика,
	ВТ_ПланАктуальный.Договор КАК Договор,
	ВТ_ПланАктуальный.Договор.НаправлениеДеятельности КАК НД,
	ВТ_ПланИсходный.СтатьяКалькуляции КАК СтатьяКалькуляции,
	СУММА(ВТ_ПланИсходный.СтоимостьВсего) / СУММА(ВТ_ПланИсходный.Количество) КАК Стоимость,
	СУММА(ВТ_ПланИсходный.СтоимостьСКоэффициентом) / СУММА(ВТ_ПланИсходный.Количество) КАК СтоимостьСКоэффициентом,
	ВТ_ПланАктуальный.Период КАК Период
ПОМЕСТИТЬ ВТ_План
ИЗ
	ВТ_ПланАктуальный КАК ВТ_ПланАктуальный
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПланИсходный КАК ВТ_ПланИсходный
		ПО ВТ_ПланАктуальный.Номенклатура = ВТ_ПланИсходный.Номенклатура
			И ВТ_ПланАктуальный.Характеристика = ВТ_ПланИсходный.Характеристика
			И ВТ_ПланАктуальный.Договор = ВТ_ПланИсходный.Договор
			И ВТ_ПланАктуальный.Период = ВТ_ПланИсходный.Период

СГРУППИРОВАТЬ ПО
	ВТ_ПланАктуальный.Номенклатура,
	ВТ_ПланАктуальный.Характеристика,
	ВТ_ПланАктуальный.Договор,
	ВТ_ПланИсходный.СтатьяКалькуляции,
	ВТ_ПланАктуальный.Договор.НаправлениеДеятельности,
	ВТ_ПланАктуальный.Период
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_План.СтатьяКалькуляции КАК СтатьяКалькуляции
ПОМЕСТИТЬ ВТ_Статьи
ИЗ
	ВТ_План КАК ВТ_План

СГРУППИРОВАТЬ ПО
	ВТ_План.СтатьяКалькуляции
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Документ.ЭтапПроизводства2_2).Распоряжение.смтр_ГруппаПродукции КАК ГАУ,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности КАК НД,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	СУММА(СебестоимостьТоваров.Количество) КАК Количество,
	ВТ_Статьи.СтатьяКалькуляции КАК СтатьяКалькуляции
ПОМЕСТИТЬ ВТ_ВыпускКоличество
ИЗ
	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров,
	ВТ_Статьи КАК ВТ_Статьи
ГДЕ
	СебестоимостьТоваров.Период >= &ДатаНачала
	И СебестоимостьТоваров.Период <= КОНЕЦПЕРИОДА(&ДатаОкончания, ДЕНЬ)
	И СебестоимостьТоваров.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
	И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	И СебестоимостьТоваров.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	И ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Документ.ЭтапПроизводства2_2).Распоряжение.смтр_ВспомогательноеПроизводство = ЛОЖЬ
	И СебестоимостьТоваров.Количество <> 0
	И НЕ СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности В
				(ВЫБРАТЬ
					ВТ_НДЗаготовка.НДЗаготовка.Значение
				ИЗ
					ВТ_НДЗаготовка)

СГРУППИРОВАТЬ ПО
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Документ.ЭтапПроизводства2_2).Распоряжение.смтр_ГруппаПродукции,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика,
	ВТ_Статьи.СтатьяКалькуляции
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ВыпускКоличество.ГАУ КАК ГАУ,
	ВТ_ВыпускКоличество.НД КАК НД,
	ВТ_ВыпускКоличество.Номенклатура КАК Номенклатура,
	ВТ_ВыпускКоличество.Характеристика КАК Характеристика,
	ВТ_ВыпускКоличество.СтатьяКалькуляции КАК СтатьяКалькуляции,
	ЕСТЬNULL(ВТ_План.Стоимость, 0) * ВТ_ВыпускКоличество.Количество КАК СтоимостьПлан,
	ЕСТЬNULL(ВТ_План.СтоимостьСКоэффициентом, 0) * ВТ_ВыпускКоличество.Количество КАК СтоимостьПланСКоэф
ПОМЕСТИТЬ ВТ_ВыпускПлан
ИЗ
	ВТ_ВыпускКоличество КАК ВТ_ВыпускКоличество
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_План КАК ВТ_План
		ПО ВТ_ВыпускКоличество.НД = ВТ_План.НД
			И ВТ_ВыпускКоличество.Номенклатура = ВТ_План.Номенклатура
			И ВТ_ВыпускКоличество.Характеристика = ВТ_План.Характеристика
			И ВТ_ВыпускКоличество.СтатьяКалькуляции = ВТ_План.СтатьяКалькуляции
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Документ.ЭтапПроизводства2_2).Распоряжение.смтр_ГруппаПродукции КАК ГАУ,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности КАК НД,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура КАК Продукция,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	СебестоимостьТоваров.СтатьяКалькуляции КАК СтатьяКалькуляции,
	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеРегл + СебестоимостьТоваров.ПостатейныеПеременныеРегл) КАК СтоимостьРегл
ПОМЕСТИТЬ ВТ_Постатейные
ИЗ
	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
ГДЕ
	СебестоимостьТоваров.Период >= &ДатаНачала
	И СебестоимостьТоваров.Период <= КОНЕЦПЕРИОДА(&ДатаОкончания, ДЕНЬ)
	И СебестоимостьТоваров.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
	И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	И СебестоимостьТоваров.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	И ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Документ.ЭтапПроизводства2_2).Распоряжение.смтр_ВспомогательноеПроизводство = ЛОЖЬ
	И СебестоимостьТоваров.ПостатейныеПостоянныеРегл + СебестоимостьТоваров.ПостатейныеПеременныеРегл <> 0
	И НЕ СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности В
				(ВЫБРАТЬ
					ВТ_НДЗаготовка.НДЗаготовка.Значение
				ИЗ
					ВТ_НДЗаготовка)

СГРУППИРОВАТЬ ПО
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Документ.ЭтапПроизводства2_2).Распоряжение.смтр_ГруппаПродукции,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура,
	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика,
	СебестоимостьТоваров.СтатьяКалькуляции
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Справочник.ПартииПроизводства).ГруппаПродукции КАК ГАУ,
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Справочник.ПартииПроизводства).НаправлениеДеятельности КАК НД,
	СебестоимостьТоваров.СтатьяКалькуляции КАК СтатьяКалькуляции,
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Справочник.ПартииПроизводства).ОсновноеИзделиеНоменклатура КАК Продукция,
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Справочник.ПартииПроизводства).ОсновноеИзделиеХарактеристика КАК Характеристика,
	СУММА(СебестоимостьТоваров.СтоимостьРегл) КАК СтоимостьРегл
ПОМЕСТИТЬ ВТ_Материалы
ИЗ
	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
ГДЕ
	СебестоимостьТоваров.Период >= &ДатаНачала
	И СебестоимостьТоваров.Период <= КОНЕЦПЕРИОДА(&ДатаОкончания, ДЕНЬ)
	И СебестоимостьТоваров.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
	И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	И СебестоимостьТоваров.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
	И ВЫРАЗИТЬ(СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.Заказ КАК Документ.ЭтапПроизводства2_2).Распоряжение.смтр_ВспомогательноеПроизводство = ЛОЖЬ
	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.Заказ ССЫЛКА Документ.ЭтапПроизводства2_2
	И НЕ СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности В
				(ВЫБРАТЬ
					ВТ_НДЗаготовка.НДЗаготовка.Значение
				ИЗ
					ВТ_НДЗаготовка)

СГРУППИРОВАТЬ ПО
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Справочник.ПартииПроизводства).ГруппаПродукции,
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Справочник.ПартииПроизводства).НаправлениеДеятельности,
	СебестоимостьТоваров.СтатьяКалькуляции,
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Справочник.ПартииПроизводства).ОсновноеИзделиеНоменклатура,
	ВЫРАЗИТЬ(СебестоимостьТоваров.Партия КАК Справочник.ПартииПроизводства).ОсновноеИзделиеХарактеристика
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_Постатейные.ГАУ КАК ГАУ,
	ВТ_Постатейные.НД КАК НД,
	ВТ_Постатейные.Продукция КАК Продукция,
	ВТ_Постатейные.Характеристика КАК Характеристика,
	ВТ_Постатейные.СтатьяКалькуляции КАК СтатьяКалькуляции,
	ВТ_Постатейные.СтоимостьРегл КАК СтоимостьРегл
ПОМЕСТИТЬ ВТ_ВыпускФакт
ИЗ
	ВТ_Постатейные КАК ВТ_Постатейные

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ВТ_Материалы.ГАУ,
	ВТ_Материалы.НД,
	ВТ_Материалы.Продукция,
	ВТ_Материалы.Характеристика,
	ВТ_Материалы.СтатьяКалькуляции,
	ВТ_Материалы.СтоимостьРегл
ИЗ
	ВТ_Материалы КАК ВТ_Материалы
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ВыпускПлан.ГАУ КАК ГАУ,
	ВТ_ВыпускПлан.НД КАК НД,
	ВТ_ВыпускПлан.Номенклатура КАК Номенклатура,
	ВТ_ВыпускПлан.Характеристика КАК Характеристика,
	ВТ_ВыпускПлан.СтатьяКалькуляции КАК СтатьяКалькуляции,
	ВТ_ВыпускПлан.СтоимостьПлан КАК СтоимостьПлан,
	ВТ_ВыпускПлан.СтоимостьПланСКоэф КАК СтоимостьПланСКоэф,
	0 КАК СтоимостьРегл,
	ВТ_ВыпускПлан.СтоимостьПлан КАК СтоимостьПланОтклонение,
	ВТ_ВыпускПлан.СтоимостьПланСКоэф КАК СтоимостьПланСКоэфОтклонение
ПОМЕСТИТЬ ВТ_ПланФакт
ИЗ
	ВТ_ВыпускПлан КАК ВТ_ВыпускПлан

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ВТ_ВыпускФакт.ГАУ,
	ВТ_ВыпускФакт.НД,
	ВТ_ВыпускФакт.Продукция,
	ВТ_ВыпускФакт.Характеристика,
	ВТ_ВыпускФакт.СтатьяКалькуляции,
	0,
	0,
	ВТ_ВыпускФакт.СтоимостьРегл,
	-ВТ_ВыпускФакт.СтоимостьРегл,
	-ВТ_ВыпускФакт.СтоимостьРегл
ИЗ
	ВТ_ВыпускФакт КАК ВТ_ВыпускФакт
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ПланФакт.ГАУ КАК ГАУ,
	ВТ_ПланФакт.НД КАК НД,
	ВТ_ПланФакт.Номенклатура КАК Номенклатура,
	ВТ_ПланФакт.Характеристика КАК Характеристика,
	ВТ_ПланФакт.СтатьяКалькуляции КАК СтатьяКалькуляции,
	СУММА(ВТ_ПланФакт.СтоимостьПлан) КАК СтоимостьПлан,
	СУММА(ВТ_ПланФакт.СтоимостьПланСКоэф) КАК СтоимостьПланСКоэф,
	СУММА(ВТ_ПланФакт.СтоимостьРегл) КАК СтоимостьРегл,
	СУММА(ВТ_ПланФакт.СтоимостьПланОтклонение) КАК СтоимостьПланОтклонение,
	СУММА(ВТ_ПланФакт.СтоимостьПланСКоэфОтклонение) КАК СтоимостьПланСКоэфОтклонение
ПОМЕСТИТЬ ВТ_Итог
ИЗ
	ВТ_ПланФакт КАК ВТ_ПланФакт

СГРУППИРОВАТЬ ПО
	ВТ_ПланФакт.ГАУ,
	ВТ_ПланФакт.НД,
	ВТ_ПланФакт.Номенклатура,
	ВТ_ПланФакт.Характеристика,
	ВТ_ПланФакт.СтатьяКалькуляции
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_Итог.ГАУ КАК ГАУ,
	ВТ_Итог.НД КАК НД,
	ВТ_Итог.Номенклатура КАК Номенклатура,
	ВТ_Итог.Характеристика КАК Характеристика,
	ВТ_Итог.СтатьяКалькуляции КАК СтатьяКалькуляции,
	ВТ_Итог.СтоимостьПлан КАК СтоимостьПлан,
	ВТ_Итог.СтоимостьПланСКоэф КАК СтоимостьПланСКоэф,
	ВТ_Итог.СтоимостьРегл КАК СтоимостьРегл,
	ВТ_Итог.СтоимостьПланОтклонение КАК СтоимостьПланОтклонение,
	ВТ_Итог.СтоимостьПланСКоэфОтклонение КАК СтоимостьПланСКоэфОтклонение,
	ВЫБОР
		КОГДА ЕСТЬNULL(ВТ_ИтогОТ.СтоимостьПлан, 0) = 0
			ТОГДА 0
		ИНАЧЕ ВЫРАЗИТЬ(100 * (ВТ_Итог.СтоимостьПлан / ВТ_ИтогОТ.СтоимостьПлан) КАК ЧИСЛО(15, 2))
	КОНЕЦ КАК ПланОтЗП,
	ВЫБОР
		КОГДА ЕСТЬNULL(ВТ_ИтогОТ.СтоимостьРегл, 0) = 0
			ТОГДА 0
		ИНАЧЕ ВЫРАЗИТЬ(100 * (ВТ_Итог.СтоимостьРегл / ВТ_ИтогОТ.СтоимостьРегл) КАК ЧИСЛО(15, 2))
	КОНЕЦ КАК ФактОтЗП
ИЗ
	ВТ_Итог КАК ВТ_Итог
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Итог КАК ВТ_ИтогОТ
		ПО ВТ_Итог.ГАУ = ВТ_ИтогОТ.ГАУ
			И ВТ_Итог.НД = ВТ_ИтогОТ.НД
			И ВТ_Итог.Номенклатура = ВТ_ИтогОТ.Номенклатура
			И ВТ_Итог.Характеристика = ВТ_ИтогОТ.Характеристика
			И (ВТ_ИтогОТ.СтатьяКалькуляции В
				(ВЫБРАТЬ
					ВТ_СтатьяОТИтого.СтатьяОТИтого.Значение
				ИЗ
					ВТ_СтатьяОТИтого))
			И (ВТ_Итог.СтатьяКалькуляции В
				(ВЫБРАТЬ
					ВТ_СтатьяПроцент.СтатьяПроцент.Значение
				ИЗ
					ВТ_СтатьяПроцент))